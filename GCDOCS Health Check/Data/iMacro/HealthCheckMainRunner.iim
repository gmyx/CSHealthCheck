' GCDOCS HEALTH CHECK: CREATED BY GUY MOREAU

SET !EXTRACT_TEST_POPUP NO
SET !ENCRYPTION TMPKEY
SET !DATASOURCE {{!VAR0}}
SET !DATASOURCE_COLUMNS 8
SET !DATASOURCE_LINE {{!VAR1}}
SET FILETOUPLOAD_PATH {{!COL7}}
SET FILETOUPLOAD_NAME {{!COL8}}
URL GOTO={{!COL5}}

' DO NOT MODIFY THE VARIABLE/COLUMN NUMBERS, CHANGE THE CALLING SCRIPT INSTEAD

' LOGIN TO SYSTEM
TAG POS=1 TYPE=INPUT:TEXT FORM=ID:LoginForm ATTR=ID:Username CONTENT={{!COL2}}
TAG POS=1 TYPE=INPUT:PASSWORD FORM=ID:LoginForm ATTR=ID:Password CONTENT={{!COL4}}
TAG POS=1 TYPE=INPUT:SUBMIT FORM=ID:LoginForm ATTR=ID:loginbutton

' SEARCH ENTERPRISE
TAG POS=1 TYPE=INPUT:TEXT FORM=ID:fulltextSearchBarForm ATTR=ID:fulltextwhere1 CONTENT=canada
TAG POS=1 TYPE=INPUT:BUTTON FORM=ID:fulltextSearchBarForm ATTR=ID:fulltextsubmitButton
WAIT SECONDS=2

' SEARCH HELP
TAG POS=4 TYPE=DIV ATTR=TXT:
TAG POS=1 TYPE=SPAN ATTR=ID:globalMenuItem_0.0
TAG POS=1 TYPE=INPUT:TEXT FORM=ID:fulltextSearchBarForm ATTR=ID:fulltextwhere1 CONTENT=collection
TAG POS=1 TYPE=A ATTR=CLASS:show-search-panel
TAG POS=1 TYPE=DIV ATTR=ID:{{!COL6}}
TAG POS=1 TYPE=INPUT:BUTTON FORM=ID:fulltextSearchBarForm ATTR=ID:fulltextsubmitButton
WAIT SECONDS=2

'GO TO PERSONAL WORKSPACE
TAG POS=1 TYPE=SPAN ATTR=ID:globalMenu_1
TAG POS=1 TYPE=SPAN ATTR=ID:globalMenuItem_1.0

'ADD A DOCUMENT
TAG POS=1 TYPE=SPAN ATTR=TXT:Add<SP>Document
TAG POS=1 TYPE=INPUT:FILE FORM=ID:CreateForm ATTR=ID:versionFile CONTENT={{FILETOUPLOAD_PATH}}{{FILETOUPLOAD_NAME}}
TAG POS=1 TYPE=INPUT:BUTTON FORM=ID:CreateForm ATTR=ID:addButton
WAIT SECONDS=2

' SEND AN E-MAIL
TAG POS=1 TYPE=A ATTR=TXT:{{FILETOUPLOAD_NAME}} EXTRACT=TXT
TAG POS=R1 TYPE=IMG ATTR=TITLE:Functions
TAG POS=R1 TYPE=DIV ATTR=TXT:E-mail
TAG POS=1 TYPE=INPUT:TEXT FORM=NAME:eLinkForm ATTR=ID:ToEmailAddresses_Name CONTENT={{!COL3}}
TAG POS=1 TYPE=INPUT:TEXT FORM=NAME:eLinkForm ATTR=ID:subject CONTENT=Test<SP>Send<SP>at<SP>:<SP>{{!NOW:dd/mm/yyyy<SP>hh:nn:ss}}
TAG POS=1 TYPE=INPUT:RADIO FORM=NAME:eLinkForm ATTR=ID:link
TAG POS=1 TYPE=TEXTAREA FORM=NAME:eLinkForm ATTR=ID:message CONTENT=Test<SP>send<SP>from<SP>automated<SP>script<SP>ran<SP>at<SP>{{!NOW:dd/mm/yyyy<SP>hh:nn:ss}}
TAG POS=1 TYPE=INPUT:SUBMIT FORM=NAME:eLinkForm ATTR=*

' DELETE THAT DOCUMENT
TAG POS=1 TYPE=INPUT:CHECKBOX FORM=NAME:BrowseViewFrm ATTR=ID:{{FILETOUPLOAD_NAME}}_checkbox CONTENT=YES
TAG POS=2 TYPE=INPUT:BUTTON FORM=NAME:BrowseViewFrm ATTR=NAME:Delete
TAG POS=1 TYPE=INPUT:BUTTON FORM=NAME:DeleteForm ATTR=ID:deleteButton
TAG POS=1 TYPE=INPUT:BUTTON FORM=NAME:myForm ATTR=ID:okButton

' GO TO SOV
URL GOTO={{!COL5}}?func=ll&objtype=148&objaction=browse
WAIT SECONDS=2

' OPEN ENTERPRISE DATA SOURCE FOLDER
TAG POS=1 TYPE=A ATTR=TXT:Enterprise<SP>Data<SP>Source<SP>Folder
WAIT SECONDS=2

' OPEN DATA FLOW MANAGER
TAG POS=1 TYPE=A ATTR=TXT:Enterprise<SP>Data<SP>Flow<SP>Manager
WAIT SECONDS=5

' OPEN PARTITION MAP
BACK
TAG POS=1 TYPE=A ATTR=TXT:Enterprise<SP>Partition<SP>Map
WAIT SECONDS=5

' LOG OUT
TAG POS=1 TYPE=SPAN ATTR=ID:globalMenu_2
TAG POS=1 TYPE=SPAN ATTR=ID:globalMenuItem_2.3

URL GOTO={{!VAR2}}